[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
ext.projectName = 'SquidStorePath'
eclipse.project.name = ext.projectName
sourceSets.test.resources.srcDirs += [ file('../../assets').path ]
javadoc.destinationDir = file("../../docs/squidstore/$name/apidocs")

dependencies {
    api project(':squidcore')
    api project(':squidgrid')
    api project(':squidpath')
    api project(':squidstore:core')
    api project(':squidstore:grid')
	api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api("com.github.tommyettinger:jdkgdxds_interop:$jdkgdxdsInteropVersion"){
        exclude module: 'jdkgdxds'
        exclude module: 'juniper'
        exclude module: 'digital'
    }

    compileOnly "org.jetbrains:annotations:$jbAnnotations"  // provides nullity annotations
    testCompileOnly "org.jetbrains:annotations:$jbAnnotations"  // provides nullity annotations

    testImplementation "junit:junit:$junitVersion"
    //testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.9.0"
    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}


import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
}

plainJavadocJar.archiveBaseName.set(project.name)

jar {
    archiveBaseName.set(project.name)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}
